Option Explicit

Private con As New ADODB.Connection

Sub Connect(ipOrServername As String, dbname As String, usrId As String, pwd As String)
    
    Dim con_string As String
    con_string = "data Source={0} ; Initial Catalog={1} ;"
    con_string = Replace(con_string, "{0}", ipOrServername)
    con_string = Replace(con_string, "{1}", dbname)

    con.Provider = "SQLOLEDB"
    con.Open ConnectionString:=con_string, UserId:=usrId, Password:=pwd

End Sub

Property Get connection() As ADODB.Connection
    Set connection = con
End Property

Function GetCursor(sql As String) As ADODB.Recordset

    'adOpenStaticは、レコード件数を保持します
    Dim rs As New ADODB.Recordset
    rs.CursorLocation = adUseClient
    rs.Open sql, con, adOpenStatic, adLockReadOnly
    Set GetCursor = rs

End Function

'ドハマリ注意！顔を取得してLOOPしながら一時テーブルを作って～なんてやるときは
'顔を取得する new SQLServer と アクションクエリを発行する new SQLServer とで変数を分ける必要があります
'ConnectionをクローズしてもOKです
'https://qa.itmedia.co.jp/qa7544490.html
Sub Execute(sql As String)

    Dim cmd As ADODB.Command
    Set cmd = New ADODB.Command
    cmd.ActiveConnection = con
    cmd.CommandText = sql
    cmd.Execute

End Sub

Sub DisConnect()
    con.Close
End Sub
