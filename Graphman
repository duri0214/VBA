Option Explicit

Private cht As Chart
Private axis As Integer

Property Get Title() As String
    Title = cht.ChartTitle.Caption
End Property
Property Let Title(value As String)
    cht.ChartTitle.Caption = value
End Property

Sub Init(sh As Worksheet, graphName As String, axis As Integer)
    Set cht = sh.ChartObjects(graphName).Chart
    sh.ChartObjects(graphName).Activate
    axis = axis
End Sub

'データ系列（＝series_idx）内の単一のデータ要素（＝data_idx）
Sub SetGraphLabel(series_idx As Integer, data_idx As Integer, rgb As Long)

    Dim p As Point
    Set p = cht.SeriesCollection(series_idx).Points(data_idx)
    p.ApplyDataLabels
    p.DataLabel.Format.AutoShapeType = msoShapeRectangle
    p.DataLabel.Format.Line.Visible = msoTrue
    p.DataLabel.Format.Line.ForeColor.rgb = rgb
    
End Sub

Sub SetGraphLabel_adjustXY(series_idx As Integer, data_idx As Integer, leftOffset As Single, topOffset As Single)

    Dim p As Point
    Set p = cht.SeriesCollection(series_idx).Points(data_idx)
    
    p.DataLabel.Left = p.DataLabel.Left + leftOffset
    p.DataLabel.Top = p.DataLabel.Top + topOffset
    
End Sub

Sub ClearGraphRange()

    Dim cnt As Integer
    Dim i As Integer
    
    'データ1行分が cht.seriescollection(i) です。それらを削除（グラフはまっしろけになる）
    cnt = cht.SeriesCollection.Count
    For i = cnt To 1 Step -1
        '先頭から削除するとナンバリングが動的に変わるため、最大Cntから消しにかかる
        cht.SeriesCollection(i).Delete
    Next i

End Sub

Sub SetGraphRange(seriesHeader As Range, hanreiRange As Range)

    Dim cnt As Integer
    Dim i As Integer
    Dim r As Range
    
    'データ1行分が cht.seriescollection(i) です。それらを削除（グラフはまっしろけになる）
    cnt = cht.SeriesCollection.Count
    For i = cnt To 1 Step -1
        '先頭から削除するとナンバリングが動的に変わるため、最大Cntから消しにかかる
        cht.SeriesCollection(i).Delete
    Next i
    
    Dim hanreiCnt As Long
    Dim seriesCnt As Long
    If axis = 0 Then
        hanreiCnt = hanreiRange.Columns.Count
        seriesCnt = seriesHeader.Rows.Count
    Else
        hanreiCnt = hanreiRange.Rows.Count
        seriesCnt = seriesHeader.Columns.Count
    End If
    
    'hanreiNameの行数ぶんだけ処理を実行します
    For i = 1 To hanreiCnt
    
        'シリーズ範囲（新たなデータ参照範囲はseriesHeaderの列sizeになります）
        If axis = 0 Then
            Set r = hanreiRange.Parent.Cells(seriesHeader(1, 1).row, hanreiRange(, i).column).Resize(seriesCnt, 1)
        Else
            Set r = hanreiRange.Parent.Cells(hanreiRange(i).row, seriesHeader(1, 1).column).Resize(1, seriesCnt)
        End If
        
        cht.SeriesCollection.NewSeries
        With cht.SeriesCollection(i)
            .Name = "=" & hanreiRange(i, 1).Resize(, hanreiRange.Columns.Count).Address(External:=True)
            .Values = "=" & r.Address(External:=True)
            .XValues = "=" & seriesHeader.Address(External:=True)
        End With
        
    Next i
    
End Sub

Sub SetGraphType(seriesNames() As String, graphType As XlChartType, axisGroup As XlAxisGroup, Optional needLabel As Boolean, Optional labelFontSize As Integer = 10, Optional rgbColor As Long)

    '▲Caution:
    '操作対象のグラフのあるシートがアクティブ化されていること
    'LOOPハズしてSeriesCollectionに対してシリーズの名前で直接指定...はできなかった（確認済み）
    
    Dim i As Integer
    Dim j As Integer
    
    For i = 0 To UBound(seriesNames)
    
        For j = 1 To cht.SeriesCollection.Count
    
            'seriesNameを見つけた！
            If cht.SeriesCollection(j).Name = seriesNames(i) Then
        
                'グラフの種類
                 cht.SeriesCollection(j).ChartType = graphType
                
                '主軸か第2軸か
                cht.SeriesCollection(j).axisGroup = axisGroup
                
                'データラベルの表示非表示
                If needLabel Then
                    cht.SeriesCollection(j).HasDataLabels = True
                    cht.SeriesCollection(j).DataLabels.ShowValue = True
                    cht.SeriesCollection(j).DataLabels.Font.Size = labelFontSize
                    cht.SeriesCollection(j).DataLabels.Position = xlLabelPositionAbove
                Else
                    cht.SeriesCollection(j).HasDataLabels = False
                End If
                
                '色の指定
                If rgbColor <> 0 Then
                    cht.SeriesCollection(j).Format.Line.ForeColor.rgb = rgbColor
                End If
                
                Exit For    'つぎのseriesNameへ（※同じseriesNameは無い前提とする）
            
            End If
    
        Next j
        
    Next i
    
End Sub

'前提: seriesNamesとrgbColorの要素数は同じであること。同じidxのseriesNameに対してrgbColorで着色される
Sub SetGraphColor(seriesNames() As String, rgbColors() As String)

    Dim i As Integer
    Dim j As Integer
    
    For i = 0 To UBound(seriesNames)
        For j = 1 To cht.SeriesCollection.Count
            'seriesNameを見つけた！
            If cht.SeriesCollection(j).Name = seriesNames(i) Then
                cht.SeriesCollection(j).Format.Fill.ForeColor.rgb = rgbColors(i)
                cht.SeriesCollection(j).Format.Line.ForeColor.rgb = rgbColors(i)
                Exit For    'つぎのseriesNameへ（※同じseriesNameは無い前提とする）
            End If
        Next j
    Next i
    
End Sub

Sub SetGraphColorpattern(seriesNames() As String, Optional ptn_type As MsoPatternType = msoPatternLightDownwardDiagonal, Optional ptn_color As Long = "255")

    Dim i As Integer
    Dim j As Integer
    
    For i = 0 To UBound(seriesNames)
        For j = 1 To cht.SeriesCollection.Count
            'seriesNameを見つけた！
            If cht.SeriesCollection(j).Name = seriesNames(i) Then
                With cht.SeriesCollection(j).Format.Fill
                    .Visible = msoTrue
                    .ForeColor.rgb = ptn_color
                    .Patterned ptn_type
                End With
            End If
            
        Next j
    Next i
    
End Sub

Sub SetGraphLineDashStyle(seriesNames() As String, Optional style As MsoLineDashStyle = msoLineDash)

    Dim i As Integer
    Dim j As Integer
    
    For i = 0 To UBound(seriesNames)
        For j = 1 To cht.SeriesCollection.Count
            'seriesNameを見つけた！
            If cht.SeriesCollection(j).Name = seriesNames(i) Then
                With cht.SeriesCollection(j).Format.Line
                    .Visible = msoTrue
                    .DashStyle = style
                End With
            End If
        Next j
    Next i
    
End Sub

Sub SetMinScale(minvalue As Integer)
    cht.Axes(xlValue).MinimumScale = minvalue
End Sub
Sub SetMaxScale(maxvalue As Long)
    cht.Axes(xlValue).MaximumScale = maxvalue
End Sub

Property Get Name() As String
    Name = cht.Name
End Property
Property Get HorizonY() As Single
    HorizonY = (cht.PlotArea.InsideTop - cht.ChartArea.Top) + cht.PlotArea.InsideHeight
End Property
Property Get VerticalX() As Single
    VerticalX = (cht.PlotArea.InsideLeft - cht.ChartArea.Left) + cht.PlotArea.InsideWidth
End Property
